from pathlib import Path
import numpy as np

temperatures = np.linspace(2.0, 6.6, 10)
SAMPLES = [f"temperature_{t:.2e}" for t in temperatures]

sample_to_temperature = {f"temperature_{t:.2e}": t for t in temperatures for t in temperatures}

results_path = "results/{sample}"

rule all:
    input:
        expand(f"{results_path}/trajectories", sample=SAMPLES ),
        expand(f"{results_path}/mfpt.png", sample=SAMPLES )


rule trajectories:
    input: 
        system = "data/system.json",
        params = "data/rate_llg.json",
        initial = "data/initial.ovf"
    params:
        temperature = lambda wc : sample_to_temperature[wc.sample],
        damping = 0.3,
        n_proc = 3,
        n_events = 10
    threads: 1
    output: trajectories = directory(f"{results_path}/trajectories")
    shell: """
    python3 scripts/rate_single.py --system {input.system} -o {output.trajectories} -i {input.initial} --n_events {params.n_events} --temp {params.temperature}
    """

mfpt_src_dir = "git/mfpt_processor"

rule clone_mfpt:
    output:
        directory(mfpt_src_dir)
    shell:"""
        rm -rf {output}
        git clone git@github.com:MSallermann/mfpt_post_processor.git {output} --depth 1 
    """

rule build_mfpt:
    input:
        mfpt_src_dir
    output:
        mfpt_bin = f"bin/mfpt"
    shell: """
    micromamba create -f {mfpt_src_dir}/environment.yml -r envs -n mfpt_env -y
    pushd {mfpt_src_dir}
    micromamba run -n mfpt_env meson setup build
    micromamba run -n mfpt_env meson compile -C build
    popd
    cp {mfpt_src_dir}/build/mfpt_processor {output.mfpt_bin}
    """

rule run_mfpt:
    input:
        trajectories = rules.trajectories.output.trajectories,
        mfpt = rules.build_mfpt.output
    output:
        f"{results_path}/mfpt.txt"
    params:
        n_disc = 100,
        min_samples = 10
    shell:"""
    {input.mfpt} {input.trajectories} --n_disc {params.n_disc} --min_samples {params.min_samples} -o {output}
    """

rule plot_sigmoid:
    input:
        mfpt = rules.run_mfpt.output
    output:
        f"{results_path}/mfpt.png"
    shell:"""
    python scripts/plot_mfpt.py --mfpt {input.mfpt} -o {output}
    """